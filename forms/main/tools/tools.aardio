import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="aardio form";right=938;bottom=638;bgcolor=16777215)
winform.add(
btnCreateLnk={cls="plus";text="创建桌面图标";left=38;top=376;right=217;bottom=418;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-23;name='FontAwesome');padding={left=18}};iconText='\uF108';notify=1;textPadding={left=50};z=5};
btnDeleteIconCache={cls="plus";text="修复桌面图标空白";left=38;top=424;right=281;bottom=466;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-23;name='FontAwesome');padding={left=18}};iconText='\uF0AD';notify=1;textPadding={left=50};z=10};
btnDnsFlush={cls="plus";text="清空 DNS 缓存";left=38;top=140;right=424;bottom=182;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF021';notify=1;textPadding={left=50};z=4};
btnGitHub={cls="plus";text="Github 网速优化";left=39;top=235;right=275;bottom=277;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF09B';notify=1;textPadding={left=50};z=2};
btnQrCode={cls="plus";text="二维码生成工具";left=38;top=282;right=274;bottom=324;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF029';notify=1;textPadding={left=50};z=8};
btnQrIdentification={cls="plus";text="二维码识别工具";left=38;top=188;right=274;bottom=230;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF2C4';notify=1;textPadding={left=50};z=1};
btnTestSpeed={cls="plus";text="当前代理服务器测速";left=38;top=46;right=256;bottom=88;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-24;name='FontAwesome');padding={left=18}};iconText='\uF0ED';notify=1;textPadding={left=50};z=6};
btnUpdateSystemTime={cls="plus";text="同步系统时间(修正 Xray 连接报错)";left=38;top=93;right=424;bottom=135;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF017';notify=1;textPadding={left=50};z=3};
btnUuid={cls="plus";text="UUID 生成工具";left=39;top=329;right=275;bottom=371;align="left";color=3947580;dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome');padding={left=18}};iconText='\uF13E';notify=1;textPadding={left=50};z=9};
lbCurrentProxyLocation={cls="static";left=257;top=59;right=741;bottom=86;color=32768;dl=1;dt=1;transparent=1;z=7}
)
/*}}*/

import style;
winform.btnQrIdentification.skin(style.plainButton);

var frmQrIdentification;
winform.btnQrIdentification.oncommand = function(id,event){
	if(frmQrIdentification&&win.isWindow(frmQrIdentification.hwnd)){
		if(win.isIconic(frmQrIdentification.hwnd)){
			frmQrIdentification.show(9/*_SW_RESTORE*/);	
		}
		
		frmQrIdentification.show();
		return;
	}
	
	frmQrIdentification = winform.loadForm("\forms\main\tools\qrIdentification.aardio");
	frmQrIdentification.show();
}

	
import process;
winform.btnGitHub.skin(style.plainButton)
winform.btnGitHub.oncommand = function(id,event){
	if(_STUDIO_INVOKED){
		winform.msgboxErr("请在发布后运行此功能");
		return;
	}
	
	process.execute(io._exepath,"/github","runas");
}


winform.btnUpdateSystemTime.skin(style.plainButton)
winform.btnUpdateSystemTime.oncommand = function(id,event){
	if(_STUDIO_INVOKED){
		winform.msgboxErr("请在发布后运行此功能");
		return;
	}
	
	winform.btnUpdateSystemTime.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
	if( process.executeWait(io._exepath,"/updateTime","runas") ){
		winform.btnUpdateSystemTime.disabledText = null;
		winform.btnUpdateSystemTime.text = '时间已同步'
		winform.setTimeout( 
			function(){
				winform.btnUpdateSystemTime.text = '同步系统时间（ 修正 Xray 连接报错 ）'
			},2000) 
	}
	else {
		winform.btnUpdateSystemTime.disabledText = null;
	} 
}

winform.btnDnsFlush.skin(style.plainButton)
winform.btnDnsFlush.oncommand = function(id,event){
	winform.btnDnsFlush.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}

	import process.popen;
	var prcs = process.popen(,"ipconfig /flushdns");
	var str = prcs.read(-1)
	
	var dnsapi = ..raw.loadDll("dnsapi.dll");
	dnsapi.DnsFlushResolverCacheB();
		
	winform.msgInfo(str);
	winform.btnDnsFlush.disabledText = null;
}


winform.btnCreateLnk.skin(style.plainButton)
winform.btnCreateLnk.oncommand = function(id,event){
	if(_STUDIO_INVOKED){
		return winform.msgboxErr("请在发布后使用此功能")
	}
	
	import fsys.lnk;  
	var lnk = fsys.lnk(); 
	lnk.description = "WinXray(Xray/V2Ray、Shadowsocks、Trojan通用客户端 )"  
	
	lnk.path = io._exepath //设置目标路径
	lnk.setIcon(io._exepath,0); //设置图标
	
	lnk.save(
		io.getSpecial(0/*_CSIDL_DESKTOP*/,"WinXray.lnk" )
	) 
	
	::Shell32.SHChangeNotify(0x8000000/*_SHCNE_ASSOCCHANGED*/,0,0,0);
	
	import com;
	com.CreateObject("Shell.Application").MinimizeAll();
}

winform.btnTestSpeed.skin(style.plainButton)
winform.btnTestSpeed.oncommand = function(id,event){
	import chrome.path;
	var chromePath = chrome.path();
	if(!chromePath){
		winform.msgFrown('抱歉，\r\n测速工具需要调用Chrome或兼容浏览器才能运行，\r\n但是当前系统未找到任何Chrome兼容浏览器！');
		return; 
	}
	
	import xray.core;
	if(!xray.core.socksProxyPort){
		winform.msgFrown('抱歉，当前未连接到任何代理服务器！');
		return; 
	}
	
	winform.btnTestSpeed.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
	
	var ipip = win.invoke(
		function(port){
			import web.rest.client;
			var restClient = web.rest.client(,"SOCKS=127.0.0.1:"+port);
			restClient._http.setTimeouts(1000,2000,2000);
			restClient.get("https://www.google.com/generate_204");
			var lastStatusCode = restClient.lastStatusCode; 
				
			if( lastStatusCode == 204 ){
					
				var ipip,err  = restClient.get("http://myip.ipip.net");
				if(ipip){
					ipip = ..string.match(ipip,"\d+\.\d+\.\d+\.\d+\A+");
					if(ipip) {
						restClient.close();
						return "当前代理服务器："+ipip;
					}
				} 
				
				restClient.close();
				return null;
			}
			restClient.close();
		},xray.core.socksProxyPort 
	)
	
	if(!ipip){
		winform.msgFrown('抱歉，当前未连接到任何代理服务器！');
		winform.btnTestSpeed.disabledText = null;
		return; 	
	}
	
	winform.lbCurrentProxyLocation.text = ipip;
	
	import process
	process.execute(chromePath,` --proxy-server="SOCKS5://127.0.0.1:`+xray.core.socksProxyPort 
		+`" --user-data-dir="` +io.fullpath("/data/chrome-socks5")+ `" --app="https://speed.cloudflare.com/"`);
	winform.btnTestSpeed.disabledText = null;
}

onActiveOutbound = function(address){
	// 设置测速按钮是否可点击（address传false时不可点击）
	winform.btnTestSpeed.disabled = !address;
	if(type.isString(address)){
		winform.lbCurrentProxyLocation.text = "当前活动的代理服务器：" + address;
	}	
} 
	
subscribe("activeOutbound",function(address){ 
	onActiveOutbound(address)
} )
if(globalActiveOutbound){
	onActiveOutbound(globalActiveOutbound)	
}

winform.btnQrCode.skin(style.plainButton)
winform.btnQrCode.oncommand = function(id,event){

	var frmChild = winform.loadForm("\forms\main\tools\qr.aardio");
	frmChild.show();
	frmChild.doModal(winform);
}

winform.btnUuid.skin(style.plainButton)
winform.btnUuid.oncommand = function(id,event){
	var frmChild = winform.loadForm("\forms\main\tools\uuid.aardio");
	frmChild.show();
	frmChild.doModal(winform);
}

winform.btnDeleteIconCache.skin(style.plainButton)
winform.btnDeleteIconCache.oncommand = function(id,event){
	import fsys;
	import process;
	
	var explorerPath = process.kill("explorer.exe")
	if( explorerPath ) {
		fsys.delete(io.appData("iconcache.db"));  
		process.execute(explorerPath);
		
		::Shell32.SHChangeNotify(0x8000000/*_SHCNE_ASSOCCHANGED*/,0,0,0);
	} 
}



winform.enableDpiScaling();
winform.show();
win.loopMessage();